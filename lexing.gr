module Lexing

from "buffer" include Buffer
from "string" include String
from "list" include List
from "char" include Char
from "result" include Result


provide record LexingState {
  mut position: Number,
  input: String
}

provide enum rec TokenType {
    Plus,
    Minus,
    Asterisk,
    Slash,
    Number,
    EndOfInput,
    OpenParen,
    CloseParen,
    Unknown,
    Identifier,
    Equals,
    Empty
}

provide record Token {
  text: String,
  kind: TokenType
}

provide let emptyToken = {
  text: "",
  kind: Empty
}

let isEndOfInput = state => state.position >= String.length(state.input)

let nextChar = (state) => {
  while (state.position < String.length(state.input)) {
    let c = String.charAt(state.position, state.input)

    if(c == ' ') {
      state.position += 1
      
      continue;
    }

    return c
  }

  return '\0'
}

let buffer = Buffer.make(256)

let tokenFromChar = (char, kind) => { text: Char.toString(char), kind: kind }
let tokenFromString = (str, kind) => { text: str, kind: kind }

provide let nextToken = state => {
  Buffer.clear(buffer)

  let mut char = nextChar(state)

  if(char == '\0') {
    return tokenFromChar(char, EndOfInput)
  }
  
  if(char == '+') {
    state.position += 1
    return tokenFromChar(char, Plus)
  }

  if(char == '*') {
    state.position += 1
    return tokenFromChar(char, Asterisk)
  }

  if(char == '-') {
    state.position += 1
    return tokenFromChar(char, Minus)
  }

  if(char == '/') {
    state.position += 1
    return tokenFromChar(char, Slash)
  }

  if(char == '(') {
    state.position += 1
    return tokenFromChar(char, OpenParen)
  }

  if(char == ')') {
    state.position += 1
    return tokenFromChar(char, CloseParen)
  }

  if(char == '=') {
    state.position += 1
    return tokenFromChar(char, Equals)
  }

  if(Char.isAsciiDigit(char)) {
    while(Char.isAsciiDigit(char)) {
      Buffer.addChar(char, buffer)
      
      state.position += 1
      char = nextChar(state)
    }

    return { text: Buffer.toString(buffer), kind: Number }
  }

  if(Char.isAsciiAlpha(char)) {
    while(Char.isAsciiAlpha(char) || Char.isAsciiDigit(char)) {
      Buffer.addChar(char, buffer)
      
      state.position += 1
      char = nextChar(state)
    }

    return { text: Buffer.toString(buffer), kind: Identifier }
  }

  return tokenFromChar(char, Unknown)
}


