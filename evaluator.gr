module Evaluator

from "./parsing.gr" include Parser
from "./tree.gr" include Tree
from "map" include Map
from "./scope.gr" include Scope

use Map.*
use Scope.*

let scope = Scope.make()

Scope.setVariable("pi", 3.14, scope)

let rec visit = (tree) => {
    match(tree) {
        Tree.NumberExpression(num) => num,
        Tree.GroupExpression(inner) => visit(inner),
        Tree.BinaryExpression(left, op, right) => visitBinary(left, op, right),
        Tree.VariableReferenceExpression(id) => visitVariableRef(id),
        Tree.FunctionCallExpression(name, args) => visitFunctionCall(name, args),
        _ => 0
    }
}
and visitFunctionCall = (name, args) => {
    match(Scope.getFunction(name, scope)) {
        Some(callback) => callback(args),
        None => fail("function " ++ name ++ " not found")
    }
}
and visitBinary = (left, op, right) => {
    let evaluatedLeft = visit(left)
    let evaluatedRight = visit(right)

    match(op) {
        "+" => evaluatedLeft + evaluatedRight,
        "-" => evaluatedLeft - evaluatedRight,
        "*" => evaluatedLeft * evaluatedRight,
        "/" => evaluatedLeft / evaluatedRight,
        "=" => visitVariableBinding(left, right),
        _ => 0
    }
}
and visitVariableRef = (id) => {
    match(Scope.getVariable(id, scope)) {
        Some(value) => value,
        None => 0
    }
}
and visitVariableBinding = (name, value) => {
    let visitedValue = visit(value)

    match(name) {
        Tree.VariableReferenceExpression(id) => {
            Scope.setVariable(id, visitedValue, scope)

            visitedValue
        },
        _ => 0
    }
}

provide let eval = (input) => {
    let tree = Parser.parse(input)

    visit(tree)
}

Scope.setFunction("power", (args) => {
    let base = visit(args[0])
    let exponent = visit(args[1])

    base ** exponent
}, scope)
