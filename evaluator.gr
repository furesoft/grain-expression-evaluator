module Evaluator

from "./parsing.gr" include Parser
from "./tree.gr" include Tree
from "map" include Map

use Map.*

provide record Scope {
    variables: Map<String, Number>,
    functions: Map<String, (Array<Tree.Expression>) => Number>
}

let variables = Map.make()

Map.set("pi", 3.14, variables)

let functions = Map.make()
let scope = {
    variables: variables,
    functions: functions
}


let rec visit = (tree) => {
    match(tree) {
        Tree.NumberExpression(num) => num,
        Tree.GroupExpression(inner) => visit(inner),
        Tree.BinaryExpression(left, op, right) => visitBinary(left, op, right),
        Tree.VariableReferenceExpression(id) => visitVariableRef(id),
        Tree.FunctionCallExpression(name, args) => visitFunctionCall(name, args),
        _ => 0
    }
}
and visitFunctionCall = (name, args) => {
    match(Map.get(name, scope.functions)) {
        Some(callback) => callback(args),
        None => fail("function " ++ name ++ " not found")
    }
}
and visitBinary = (left, op, right) => {
    let evaluatedLeft = visit(left)
    let evaluatedRight = visit(right)

    match(op) {
        "+" => evaluatedLeft + evaluatedRight,
        "-" => evaluatedLeft - evaluatedRight,
        "*" => evaluatedLeft * evaluatedRight,
        "/" => evaluatedLeft / evaluatedRight,
        "=" => visitVariableBinding(left, right),
        _ => 0
    }
}
and visitVariableRef = (id) => {
    match(Map.get(id, scope.variables)) {
        Some(value) => value,
        None => 0
    }
}
and visitVariableBinding = (name, value) => {
    let visitedValue = visit(value)

    match(name) {
        Tree.VariableReferenceExpression(id) => {
            Map.set(id, visitedValue, scope.variables)
            
            visitedValue
        },
        _ => 25
    }
}

provide let eval = (input) => {
    let tree = Parser.parse(input)

    visit(tree)
}

Map.set("power", (args) => {
    let base = visit(args[0])
    let exponent = visit(args[1])

    base ** exponent
}, functions)
