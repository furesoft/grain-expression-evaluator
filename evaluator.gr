module Evaluator

from "./parsing.gr" include Parser
from "./tree.gr" include Tree
from "map" include Map
from "./scope.gr" include Scope
from "array" include Array
from "number" include Number

use Map.*
use Scope.*

provide let rootScope = Scope.make()

let evaluateBool = (cond) => match(cond) {
    true => 1,
    false => 0
}

let isNumberInSet = (set, value) => {
    match(Scope.getSet(set, rootScope)) {
        Some(s) => s(value),
        None => false
    }
}

let rec visit = (tree, scope) => {
    match(tree) {
        Tree.NumberExpression(num) => num,
        Tree.GroupExpression(inner) => visit(inner, scope),
        Tree.BinaryExpression(left, op, right) => visitBinary(left, op, right, scope),
        Tree.VariableReferenceExpression(id) => visitVariableRef(id, scope),
        Tree.FunctionCallExpression(name, args) => visitFunctionCall(name, args, scope),
        _ => 0
    }
}
and visitFunctionCall = (name, args, scope) => {
    match(Scope.getFunction(name, scope)) {
        Some(callback) => callback(args),
        None => fail("function " ++ name ++ " not found")
    }
}
and visitBinary = (left, op, right, scope) => {
    if(op == "=") {
        return visitVariableBinding(left, right, scope)
    }

    let evaluatedLeft = visit(left, scope)
    let evaluatedRight = visit(right, scope)

    return match(op) {
        "+" => evaluatedLeft + evaluatedRight,
        "-" => evaluatedLeft - evaluatedRight,
        "*" => evaluatedLeft * evaluatedRight,
        "/" => evaluatedLeft / evaluatedRight,
        "%" => evaluatedLeft % evaluatedRight,
        "<" => evaluateBool(evaluatedLeft < evaluatedRight),
        ">" => evaluateBool(evaluatedLeft > evaluatedRight),
        "==" => evaluateBool(evaluatedLeft == evaluatedRight),
        _ => 0
    }
}
and visitVariableRef = (id, scope) => {
    match(Scope.getVariable(id, scope)) {
        Some(value) => value,
        None => fail("Variable " ++ id ++ " not found")
    }
}
and visitVariableBinding = (name, value, scope) => {
    match(name) {
        Tree.VariableReferenceExpression(id) => {
            let visitedValue = visit(value, scope)
            Scope.setVariable(id, visitedValue, scope)

            visitedValue
        },
        Tree.FunctionCallExpression(name, parameter) => {
            Scope.setFunction(name, (args) => {
                if(Array.length(args) != Array.length(parameter)) {
                    fail("argument mismatch for function " ++ name)
                }

                let funcScope = Scope.fromParent(scope)
                let evaluatedArgs = Array.map(arg => visit(arg, funcScope), args)
                
                Array.forEachi((p, i) => {
                    match(p){
                        Tree.VariableReferenceExpression(pName) => {
                            Scope.setVariable(pName, evaluatedArgs[i], funcScope)
                        },
                        _ => fail("error")
                    }
                }, parameter)

                visit(value, funcScope)
            }, scope)
            0
        },
        _ => 0
    }
}

Scope.setVariable("pi", 3.14, rootScope)

Scope.defineSet("N", value => Number.isInteger(value) && value >= 0, rootScope)
Scope.defineSet("Z", value => Number.isInteger(value), rootScope)
Scope.defineSet("Q", value => Number.isFloat(value), rootScope)

Scope.setFunction("power", (args) => {
    let scope = Scope.fromParent(rootScope)
    let base = visit(args[0], scope)
    let exponent = visit(args[1], scope)
    
    base ** exponent
}, rootScope)

provide let eval = (input) => {
    let tree = Parser.parse(input)

    visit(tree, rootScope)
}
