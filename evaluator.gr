module Evaluator

from "./parsing.gr" include Parser
from "./tree.gr" include Tree
from "map" include Map
from "./scope.gr" include Scope
from "array" include Array

use Map.*
use Scope.*

let rootScope = Scope.make()

Scope.setVariable("pi", 3.14, rootScope)

let rec visit = (tree) => {
    match(tree) {
        Tree.NumberExpression(num) => num,
        Tree.GroupExpression(inner) => visit(inner),
        Tree.BinaryExpression(left, op, right) => visitBinary(left, op, right),
        Tree.VariableReferenceExpression(id) => visitVariableRef(id),
        Tree.FunctionCallExpression(name, args) => visitFunctionCall(name, args),
        _ => 0
    }
}
and visitFunctionCall = (name, args) => {
    // ToDo: implement function call for custom defined functions
    match(Scope.getFunction(name, rootScope)) {
        Some(callback) => callback(args),
        None => fail("function " ++ name ++ " not found")
    }
}
and visitBinary = (left, op, right) => {
    if(op == "=") {
        return visitVariableBinding(left, right)
    }

    let evaluatedLeft = visit(left)
    let evaluatedRight = visit(right)

    return match(op) {
        "+" => evaluatedLeft + evaluatedRight,
        "-" => evaluatedLeft - evaluatedRight,
        "*" => evaluatedLeft * evaluatedRight,
        "/" => evaluatedLeft / evaluatedRight,
        _ => 0
    }
}
and visitVariableRef = (id) => {
    match(Scope.getVariable(id, rootScope)) {
        Some(value) => value,
        None => fail("Variable " ++ id ++ " not found")
    }
}
and visitVariableBinding = (name, value) => {
    match(name) {
        Tree.VariableReferenceExpression(id) => {
            let visitedValue = visit(value)
            Scope.setVariable(id, visitedValue, rootScope)

            visitedValue
        },
        Tree.FunctionCallExpression(name, args) => {
            Scope.setFunction(name, (a) => {
               42
            }, rootScope)
            0
        },
        _ => 0
    }
}

provide let eval = (input) => {
    let tree = Parser.parse(input)

    visit(tree)
}

Scope.setFunction("power", (args) => {
    let base = visit(args[0])
    let exponent = visit(args[1])

    base ** exponent
}, rootScope)
